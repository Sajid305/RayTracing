<!DOCTYPE html>
<html>
<head>
    <meta http-equiv="Content-Type" content="text/html;charset=utf-8" >
    <title>Ray tracer</title>

    <script id="vertex-shader" type="x-shader/x-vertex">
        attribute vec4 vPosition;

        void main() 
        {
            gl_Position = vPosition;
        } 
    </script>

    <script id="fragment-shader" type="x-shader/x-fragment">
        precision mediump float;
        
        #define RED vec4(1.0, 0.0, 0.0, 1.0);
        #define BLUE vec4(0.0, 0.0, 1.0, 1.0);
        
        // So we can do loops
        #define MAX_NUMBER_OF_OBJECTS 10
        #define MAX_NUMBER_OF_LIGHTS 10
        
        #define SPHERE_TYPE 0
        
        uniform vec2 resolution;
        
        // The scene in 2d textures
		// the id of objects as well as the type.
		uniform sampler2D objects;
		// the position of the objects a vec4 to allow for one free parameter
		uniform sampler2D objectPositions;
		// the materials of the object format to be decided.
		uniform sampler2D objectColors;
		// more material properties so that each pixel represents a unique object
		uniform sampler2D objectMaterials;
		// the light positions
		uniform sampler2D lightPositions;
		// the light colors
		uniform sampler2D lightColors;
        
        
        // The number of objects in the scene
		uniform int numObjects;
		uniform float objectTextureSize;

		uniform int numLights;
		uniform float lightTextureSize;
        
        // Texture coordinates are in the range [0, 1] and [0, 1].
        // So in order to access the 0-based-indexed elements the proper convertion must be done
        // ---------
        // | 2 | 3 |
        // | 0 | 1 |
        // ---------
        // This is the way texels are organized
        
        vec2 convertToTexelCoordinateSystem(in int objectID, in int numberObjects, in float textureSize) {
           if(numberObjects == 1)
                return vec2(0.5, 0.5);
           
           float step = 1.0 / textureSize;
           float col = mod (float(objectID) + 0.1, textureSize ) ;
           float x =  col * step + (step / 2.0);
           
           int row = int(float(objectID) / textureSize);
           float y =  float(row) * step + (step / 2.0);
           
            return vec2(x, y);
        }
            
        // Test if a ray hits a specific sphere
        bool intersectSphere(in int sphereIndexInTexture, in vec3 cameraLocation, in vec3 direction, inout float closestIntersectionDistance, inout int closestObjectID ) {
            // First some background 
            // Source: https://en.wikipedia.org/wiki/Lineâ€“sphere_intersection
            // Equation for a sphere
            // ||x - c||^2 = r^2
            //      where   c is the center point
            //              r is the radius
            //              x is a point in the sphere
            
            // Equation for a line starting at o 
            // x = o + dl
            //      where   d is the distance along line from starting point
            //              l is the direction of a line
            //              x is a point on the line
            
            // Equations combined and rearranged
            // d^2(l.l) + 2d(l.(o-c)) + (o-c).(o-c) - r^2 = 0
            // which has the form of the quadatric formula
            // with     a = (l.l) = ||l||^2 = 1     Because l is a unit vector
            //          b = 2 (l.(o - c))
            //          c = (o-c).(o-c) - r^2 = ||o-c||^2 - r^2
            // 
            // We now solve for d.

            // Enough theory
            
            // Get information about the sphere
            vec2 texelCoordinates = convertToTexelCoordinateSystem(sphereIndexInTexture, numObjects, objectTextureSize);
            vec4 sphere = texture2D(objectPositions, texelCoordinates);
            
            
            // Compute if there is an intersection
            float b = 2.0 * dot(direction, cameraLocation - sphere.xyz);
            float c = dot( cameraLocation - sphere.xyz, cameraLocation - sphere.xyz ) - sphere.w * sphere.w;
            
            float insideSqrt = b * b - 4.0 * c;
            
            // If the square product of what is inside the radical is negative, there is no solution.
            if(insideSqrt < 0.0)
                return false;
                
            float solutionD = (-b + sqrt(insideSqrt)) / 2.0;
            
            if(solutionD < 0.0 || solutionD > closestIntersectionDistance)
                return false; 
            
             
            closestIntersectionDistance = solutionD;
            closestObjectID = sphereIndexInTexture;
            
            return true;
        }
        
        // Calculates if there is an intersection from the cameraLocation in direction 'direction',
        // If there is one, returns true and save the intersection values in closestIntersectionDistance and closestObjectID
        // Where was the intersection and with whom
        bool intersectWorld(in vec3 cameraLocation, in vec3 direction, inout float closestIntersectionDistance, inout int closestObjectID){
            bool intersectionHasOccurred = false;
           
           
            // Loop through all the elements to see the type
            for(int i = 0; i < MAX_NUMBER_OF_OBJECTS; i++) {
               
                // That's the limit my friend
                if( i >= numObjects ) break;
                    
                // Get object type   
                vec2 texelCoordinates = convertToTexelCoordinateSystem(i, numObjects, objectTextureSize);
                int objectType = int( texture2D( objects, texelCoordinates ).y * 256.0 );
                  
                  
                // THe only type now is sphere, but more types can be added!
                if(objectType == SPHERE_TYPE) {
                    if (intersectSphere( i, cameraLocation, direction, closestIntersectionDistance, closestObjectID ))
                        intersectionHasOccurred = true;
                }
                 
            }
            
            return intersectionHasOccurred;
        }
        
        // Computes the normal of the plane that intersects a given point in a sphere
        vec3 computeSphereNormal(in vec3 pointInSphere, in vec4 spherePosition){
            return normalize( pointInSphere - spherePosition.xyz ); 
        }
        
        // Computes the normal of the plane that intersects a given point in a given object
        vec3 computeNormal(in vec3 intersection, in int objectID){
            vec2 myTexelCoordinate = convertToTexelCoordinateSystem( objectID , numObjects, objectTextureSize);
            int objectType = int( texture2D(objects, myTexelCoordinate).y * 256.0 );
            
            if(objectType == SPHERE_TYPE)
                return computeSphereNormal(intersection, texture2D( objectPositions, myTexelCoordinate ));
                
            // Unsupported object type
            return vec3(0.0, 0.0, 0.0);
        }
        
        vec4 computeLight(in vec3 cameraLocation, in vec3 direction, in float closestIntersectionDistance, in int closestObjectID) {
            
            // Compute normal in object
            vec3 intersection = cameraLocation + direction * closestIntersectionDistance;
            vec3 objectNormalVector = computeNormal(intersection, closestObjectID);
            
            vec4 colorLightToAdd = vec4(0.5, 0.5, .5, 1.0);
            
            // Loop all the lights
            for(int i = 0; i < MAX_NUMBER_OF_LIGHTS; i++) {
                
                if(i >= numLights) break;
                
                
         
                /// Calculate distance to light
                // Get position of light
                vec2 lightTexelCoord = convertToTexelCoordinateSystem(i, numLights, lightTextureSize);
                vec3 lightPosition = texture2D( lightPositions, lightTexelCoord ).xyz;
                
                float distanceToLight = distance(intersection, lightPosition);
                
                float lightDistanceFactorA = .2;
                float lightDistanceFactorB = .3 * distanceToLight;
                float lightDistanceFactorC = .1 * distanceToLight * distanceToLight;
                
                float lightDistanceFactor = 1.0 / ( lightDistanceFactorA + lightDistanceFactorB + lightDistanceFactorC);
                   
                colorLightToAdd *= lightDistanceFactor;
                
            }
            
            return colorLightToAdd;
            
        }
        
        void main() {
             
            vec4 backgroundColor = vec4(0.65, 0.65, 0.1, 1.0);
            vec4 color = backgroundColor;
            
  			// Where camera is located
			vec3 cameraLocation = vec3(0.0,0.0,-4.0);
            // Direction has the range [-1,1] in x coordinates
            // and [-1,1] in y coordinates
			vec3 direction = normalize(vec3(( gl_FragCoord.xy / resolution - 0.5)  * 2.0, 1.0));
            
            
            // Prepare to intersect world
            float closestIntersectionDistance = 10000.0;
            int closestObjectID = -1;
            
            if( intersectWorld( cameraLocation, direction, closestIntersectionDistance, closestObjectID ) ){
                // Houston, we found something
                
                // Get the color of the object with closest id and draw it!
                vec2 myTexelCoordinate = convertToTexelCoordinateSystem( closestObjectID , numObjects, objectTextureSize);
                vec4 objectColor = texture2D(objectColors, myTexelCoordinate);
                
                // Add light
                color = computeLight( cameraLocation, direction, closestIntersectionDistance, closestObjectID );
                
                //TODO mix lights
                color = (color / 2.0) + (objectColor / 2.0);
                
                // Add reflection
                
                
            } 
                            
            
            gl_FragColor = color;
            
            
        }
        

        
    </script>

    <script type="text/javascript" src="./Common/webgl-utils.js"></script>
    <script type="text/javascript" src="./Common/initShaders.js"></script>
    <script type="text/javascript" src="./Common/MV.js"></script>
    <script type="text/javascript" src="raytracer.js"></script>
    <script type="text/javascript" src="objectsDefinition.js"></script>
    <script type="text/javascript" src="objectsDeclaration.js"></script>
</head>  

    <body>
        <canvas id="gl-canvas" width="512" height="512">
        Oops ... your browser doesn't support the HTML5 canvas element
        </canvas>
        <p>
            <button id = "Button1">Increase camera x position</button>
            <button id = "Button2">Decrease camera x position</button>
        </p>
        <p>
            <button id = "Button3">Increase camera y position</button>
            <button id = "Button4">Decrease camera y position</button>
        </p>
        <p>
            <button id = "Button5">Increase camera z position</button>
            <button id = "Button6">Decrease camera z position</button>
        </p>
    </body>

</html>
